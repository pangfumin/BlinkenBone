
	.title sum40 - sum up numbers on PDP-11/40

	; This program reads n >= 0 from console, sums up the first n nonnegative
	; integers, halts and displays the result on console.
	; The sum turns out to be n*(n+1)/2, which does not fit in a word for
	; n > sqrt(2N).  But, since n < 2^16, the result is < 2^32 and fits in two
	; words. Registers R1 and R2 hold the high respective low word of the sum.
	; R0 counts from 1 to R3.

	; This program is for PDP-11/40, which displays R0 on the console after HALT

sr	=	177570	; switch  register

	.asect
	.=1000		; program loads at 1000

start:
	mov	#400,sp ; no stack is used, but avoid "red stack trap" in SimH

			; set up loop invariant:
	mov	@#sr,r3 ; r3 = n (from switch register)
	clr	r0	; r0 = 0
	clr	r1	; r1_r2 = 0 ;
	clr	r2
loop:			; do {
	inc	r0	;   r0++ ;
	add	r0,r2	;   r1_r2 += r0 ;
	adc	r1	;
	cmp	r0,r3	; } while  (r0 != r3) ;
	bne	loop

disphi:
	mov	r1,r0	; display high word of sum
	halt		; console operator must press continue switch now!
displo:
	mov	r2,r0	; display low word of sum
	halt		; console operator can enter new 'n' now and continue

	br	start	; execute program again
